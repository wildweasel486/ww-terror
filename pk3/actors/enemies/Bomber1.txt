ACTOR Bomber1 : WWTBadGuy
{
	YScale 0.35
	XScale 0.4
	Health 150
	Speed 12
	MeleeThreshold 192
	SeeSound "Sights/Male03"
	PainSound "Pains/Male03"
	DeathSound "Deaths/Male02"
	DropItem "GrenadePickup",84
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.0
	DamageFactor "PlayerGrenade",0.5
	DamageFactor "PlayerAKGrenade",0.5
	DamageFactor "PlayerMGL",0.5
	Obituary "%o learns why the prison escape rate is so high lately."
	HitObituary "%o was conked on the head by a guy with a pipebomb."
	States
	{
	Spawn:
		BOM1 A 1 A_Look
		Loop
	See:
		BOM1 BCDE 6 A_Chase
		Loop
	Missile:
		BOM1 A 10 Fast A_FaceTarget
		BOM1 F 0 A_PlaySound("Grenade/Throw",CHAN_WEAPON)
		BOM1 F 5 A_FaceTarget
		BOM1 G 5 A_CustomMissile("EnemyPipebomb",40,0,frandom(-2.0,2.0))
		BOM1 H 5
		Goto See
	Melee:
		BOM1 F 3 A_FaceTarget
		BOM1 G 4 A_CustomMeleeAttack(20,"Grenade/melee","melee/swing")
		BOM1 H 10
		BOM1 B 5
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		BOM1 I 0
		Goto Tased
	Pain:
		BOM1 I 3 A_Pain
		Goto See
	Death:
		BOM1 J 3 A_Scream
		BOM1 K 3
		BOM1 L 3
		BOM1 M 3 A_PlaySound("Misc/Bodyfall")
		BOM1 N 3 A_NoBlocking
		BOM1 O -1
		Stop
	}
}