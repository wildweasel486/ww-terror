// UAV type 1
// Sprite source: System Shock by Origin/Looking Glass Studios

ACTOR UAV1 : WWTBadGuy replaces Cacodemon
{
	Health 300
	Radius 31
	Height 56
	Mass 400
	Speed 5
	Painchance 255
	PainSound "Bullet/HitMetal"
	ActiveSound "UAV1/Active"
	SeeSound "UAV1/Active"
	DeathSound "UAV1/Death"
	Obituary "%o was eliminated remotely by some guy at a computer."
	MONSTER
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	+DONTRIP
	+SPAWNCEILING
	States
	{
	Spawn:
		UAV1 ABCDE 5 A_Look
		Loop
	See:
		UAV1 A 1 A_Chase
		UAV1 A 1 A_Chase("","")
		UAV1 A 1 A_Chase("","")
		UAV1 A 1 A_Chase("","")
		UAV1 B 1 A_Chase
		UAV1 B 1 A_Chase("","")
		UAV1 B 1 A_Chase("","")
		UAV1 B 1 A_Chase("","")
		UAV1 C 1 A_Chase
		UAV1 C 1 A_Chase("","")
		UAV1 C 1 A_Chase("","")
		UAV1 C 1 A_Chase("","")
		UAV1 D 1 A_Chase
		UAV1 D 1 A_Chase("","")
		UAV1 D 1 A_Chase("","")
		UAV1 D 1 A_Chase("","")
		UAV1 E 1 A_Chase
		UAV1 E 1 A_Chase("","")
		UAV1 E 1 A_Chase("","")
		UAV1 E 1 A_Chase("","")
		Loop
	Missile:
		UAV1 B 0 A_Jump(64, "SuperMissile")
		UAV1 B 12 A_FaceTarget
		UAV1 F 0 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, 27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, -27)
		UAV1 ABCDE 5
		Goto See
	SuperMissile:
		UAV1 B 0 A_PlaySound("UAV1/BayOpen", CHAN_ITEM)
		UAV1 BBB 12 A_FaceTarget
		UAV1 F 0 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, 27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, -27)
		UAV1 B 10 Fast
		UAV1 F 0 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, 27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, -27)
		UAV1 B 10 Fast
		UAV1 F 0 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, 27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, -27)
		UAV1 B 10 Fast
		UAV1 F 0 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, 27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile", 26, -27)
		UAV1 B 10
		UAV1 ABCDE 5
		Goto See
	Pain.PlayerElectricGrenade:
	Pain.PlayerTaser:
	Pain.EnemyTaser:
		UAV1 A 0
	Tased:
		"----" A 0 A_ChangeFlag ("NoPain", 1)
		"----" A 0 A_Gravity
		"----" A 0 A_ChangeFlag ("FloatBob", 0)
		"----" A 0 A_SetUserVar ("user_stuntime", (STUNTIME / 2))
	TasedLoop:
		"----" A 1 BRIGHT
		"----" A 1
		"----" A 0 A_JumpIf (user_stuntime == 0, 2)
		"----" A 0 A_SetUserVar ("user_stuntime", user_stuntime - 1)
		Loop
		"----" A 0 A_NoGravity
		"----" A 0 A_ChangeFlag ("FloatBob", 1)
		"----" A 0 A_ChangeFlag ("NoPain", 0)
		Goto See
	Pain:
		UAV1 GGGGGGGGGG 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(3.0,6.0),frandom(0.0,359.9),SXF_NOCHECKPOSITION)
	Pain.Fire:
	Pain.PlayerGrenadeFire:
		UAV1 G 1 A_Pain
		Goto See
	Death.PlayerGrenadeFire:
		"----" A 0 A_GiveToTarget("GrenadeExp", 1)
	Death.Fire:
	Death:
		UAV1 A 0 A_StopSound(CHAN_BODY)
		UAV1 G 2 A_Scream
		UAV1 HIJ 2
		UAV1 KLMNOP 4 A_SpawnItemEx("DIExplosion",frandom(-20.0,20.0),frandom(-20.0,20.0),frandom(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		UAV1 Q 4 A_Explode(32,128)
		TNT1 A 1 A_SpawnItemEx("DIExplosionLarge",frandom(-20.0,20.0),frandom(-20.0,20.0),frandom(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR UAV1MiniMissile : FastProjectile
{
	Radius 11
	Height 8
	Speed 20
	Damage (10)
	PROJECTILE
	SeeSound "UAV1/Missile"
	DeathSound "UAV1/MissileHit"
	MissileType "MiniMissileTrail"
	MissileHeight 12
	Scale 0.5
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	Death:
		MISL A 0 A_SetScale(1.0)
		MISL B 4 Bright A_Scream
		MISL CD 4 Bright
		Stop
	}
}
	
	
ACTOR MiniMissileTrail
{ 
	Alpha 1.0
	RenderStyle Add
	Speed 0
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	Scale 0.1
	States
	{
	Spawn:
		RTRL ABC 2 BRIGHT
		Stop
	}
}