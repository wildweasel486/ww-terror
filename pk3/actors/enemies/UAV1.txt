// UAV type 1
// Sprite source: System Shock by Origin/Looking Glass Studios

ACTOR UAV1 : WWTRobot replaces Cacodemon
{
	Health 200
	Speed 5
	FloatSpeed 1
	ActiveSound "UAV1/Active"
	SeeSound "UAV1/Active"
	DeathSound "UAV1/Death"
	Obituary "%o was eliminated remotely by some guy at a computer."
	+SPAWNCEILING
	States
	{
	Spawn:
		UAV1 ABCDE 5 A_Look
		Loop
	See:
		UAV1 A 1 A_Chase
		UAV1 AAA 1 A_Chase("","")
		UAV1 B 1 A_Chase
		UAV1 BBB 1 A_Chase("","")
		UAV1 C 1 A_Chase
		UAV1 CCC 1 A_Chase("","")
		UAV1 D 1 A_Chase
		UAV1 DDD 1 A_Chase("","")
		UAV1 E 1 A_Chase
		UAV1 EEE 1 A_Chase("","")
		Loop
	Missile:
		UAV1 B 0 A_Jump(64,"SuperMissile")
		UAV1 B 12 A_FaceTarget
		UAV1 F 0 A_CustomMissile("UAV1MiniMissile",26,27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile",26,-27)
		UAV1 ABCDE 5
		Goto See
	SuperMissile:
		UAV1 B 0 A_PlaySound("UAV1/BayOpen",CHAN_ITEM)
		UAV1 BBB 12 A_FaceTarget
		UAV1 F 0 A_CustomMissile("UAV1MiniMissile",26,27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile",26,-27)
		UAV1 B 10 Fast
		UAV1 F 0 A_CustomMissile("UAV1MiniMissile",26,27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile",26,-27)
		UAV1 B 10 Fast
		UAV1 F 0 A_CustomMissile("UAV1MiniMissile",26,27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile",26,-27)
		UAV1 B 10 Fast
		UAV1 F 0 A_CustomMissile("UAV1MiniMissile",26,27)
		UAV1 F 3 BRIGHT A_CustomMissile("UAV1MiniMissile",26,-27)
		UAV1 B 10
		UAV1 ABCDE 5
		Goto See
	Pain.PlayerTaser:
		UAV1 A 0
		Goto Tased
	Pain:
		UAV1 GGGGGGGGGG 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(3.0,6.0),frandom(0.0,359.9),SXF_NOCHECKPOSITION)
	Pain.Fire:
		UAV1 G 1 A_Pain
		Goto See
	Death:
		UAV1 A 0 A_StopSound(CHAN_BODY)
		UAV1 G 2 A_Scream
		UAV1 HIJ 2
		UAV1 KLMNOP 4 A_SpawnItemEx("DIExplosion",frandom(-20.0,20.0),frandom(-20.0,20.0),frandom(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		UAV1 Q 4 A_Explode(32,128)
		TNT1 A 1 A_SpawnItemEx("DIExplosionLarge",frandom(-20.0,20.0),frandom(-20.0,20.0),frandom(0,32.0),0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR UAV1MiniMissile : FastProjectile
{
	Radius 11
	Height 8
	Speed 20
	Damage (10)
	PROJECTILE
	SeeSound "UAV1/Missile"
	DeathSound "UAV1/MissileHit"
	MissileType "MiniMissileTrail"
	MissileHeight 12
	Scale .5
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	Death:
		MISL A 0 A_SetScale(1.0)
		MISL B 4 Bright A_Scream
		MISL BCD 4 Bright
		Stop
	}
}

ACTOR MiniMissileTrail
{
	RenderStyle Add
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	Scale .1
	States
	{
	Spawn:
		RTRL ABC 2 BRIGHT
		Stop
	}
}