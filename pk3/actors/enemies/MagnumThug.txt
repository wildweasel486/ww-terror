ACTOR MagnumThug : WWTBadGuy
{
	YScale 0.35
	XScale 0.4
	Health 50
	Speed 16
	SeeSound "Sights/Male06"
	PainSound "Pains/Male06"
	DeathSound "Deaths/Male06"
	DropItem "9mmAmmoLarge"
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.3
	DamageFactor "PlayerPistol",0.5
	DamageFactor "PlayerUzi",0.5
	DamageFactor "PlayerMelee",2.0
	Obituary "%o's death was an Executive Decision."
	+MISSILEMORE
	States
	{
	Spawn:
		MTHG A 1 A_LookEx(0,0,0,0,0,"SeeFirst")
		Loop
	SeeFirst:
		MTHG B 6 Fast A_Chase
		MTHG B 0 A_JumpIf(CallACS("IsNightmare") >= 4,2)
		MTHG B 0 A_SetSpeed(8.0/3.0)
		Goto See+7
		MTHG B 0 A_SetSpeed(16.0/3.0)
		Goto See2+4
	See:
		MTHG B 0 A_JumpIf(CallACS("IsNightmare") >= 4,"See2")
		MTHG B 0 A_SetSpeed(8.0/3.0)
		MTHG B 1 A_Chase
		MTHG BBBBB 1 A_Chase("","")
		MTHG C 1 A_Chase
		MTHG CCCCC 1 A_Chase("","")
		MTHG D 1 A_Chase
		MTHG DDDDD 1 A_Chase("","")
		MTHG E 1 A_Chase
		MTHG EEEEE 1 A_Chase("","")
		Loop
	See2:
		MTHG B 0 A_SetSpeed(16.0/3.0)
		MTHG B 1 A_Chase
		MTHG BB 1 A_Chase("","")
		MTHG C 1 A_Chase
		MTHG CC 1 A_Chase("","")
		MTHG D 1 A_Chase
		MTHG DD 1 A_Chase("","")
		MTHG E 1 A_Chase
		MTHG EE 1 A_Chase("","")
		Loop
	Missile:
		MTHG F 10 Fast A_FaceTarget
		MTHG G 0 A_PlaySound("Beretta/EnemyFire",CHAN_WEAPON)
		MTHG G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MTHG G 2 BRIGHT A_CustomMissile("EnemyPistolTracer",48,0,frandom(-1.0,1.0))
		MTHG HF 5 Fast A_FaceTarget
		MTHG G 0 A_PlaySound("Beretta/EnemyFire",CHAN_WEAPON)
		MTHG G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MTHG G 2 BRIGHT A_CustomMissile("EnemyPistolTracer",48,0,frandom(-1.0,1.0))
		MTHG H 5
		MTHG F 4
		Goto See
	Pain.PlayerPistol:
	Pain.PlayerUzi:
		MTHG IIIIIIIIII 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(3.0,6.0),frandom(0.0,359.9),SXF_NOCHECKPOSITION)
		MTHG I 3 A_PlaySound("Bullet/HitMetal")
		Goto See
	Pain.PlayerTaser:
		MTHG I 0
		Goto Tased
	Pain:
		MTHG I 3 A_Pain
		Goto See
	Death:
		MTHG J 3 A_Scream
		MTHG KLM 3
		MTHG N 3 A_NoBlocking
		MTHG O 3 A_PlaySound("Misc/Bodyfall")
		MTHG P -1
		Stop
	}
}