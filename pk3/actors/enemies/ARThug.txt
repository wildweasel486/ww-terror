ACTOR ARThug : WWTBadGuy
{
	YScale 0.3
	XScale 0.35
	Health 90
	Speed 11
	SeeSound "Sights/Male07"
	PainSound "Pains/Male07"
	DeathSound "Deaths/Male07"
	DropItem "WWTRifleSpawner" 84
	DropItem "762mmAmmo" 84
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.3
	DamageFactor "PlayerPistol",0.4
	DamageFactor "PlayerUzi",0.4
	DamageFactor "PlayerShotgun",0.4
	DamageFactor "PlayerAR",0.9
	DamageFactor "PlayerM60",0.9
	DamageFactor "PlayerMagnum",0.7
	DamageFactor "PlayerMelee",2.0
	Obituary "%o was hit fast and sharp by a commando."
	+MISSILEMORE
	States
	{
	Spawn:
		ATHG A 1 A_LookEx(0,0,0,0,0,"SeeFirst")
		Loop
	SeeFirst:
		ATHG B 4 Fast A_Chase
		ATHG B 0 A_JumpIf(CallACS("IsNightmare") >= 4,2)
		ATHG B 0 A_SetSpeed(2.75)
		Goto See+6
		ATHG A 0 A_SetSpeed(5.5)
		Goto See2+3
	See:
		ATHG B 0 A_JumpIf(CallACS("IsNightmare") >= 4,"See2")
		ATHG B 0 A_SetSpeed(2.75)
		ATHG B 1 A_Chase
		ATHG BBB 1 A_Chase("","")
		ATHG B 1 A_Chase
		ATHG BBB 1 A_Chase("","")
		ATHG C 1 A_Chase
		ATHG CCC 1 A_Chase("","")
		ATHG C 1 A_Chase
		ATHG CCC 1 A_Chase("","")
		ATHG D 1 A_Chase
		ATHG DDD 1 A_Chase("","")
		ATHG D 1 A_Chase
		ATHG DDD 1 A_Chase("","")
		ATHG E 1 A_Chase
		ATHG EEE 1 A_Chase("","")
		ATHG E 1 A_Chase
		ATHG EEE 1 A_Chase("","")
		Loop
	See2:
		ATHG A 0 A_SetSpeed(5.5)
		ATHG B 1 A_Chase
		ATHG B 1 A_Chase("","")
		ATHG B 1 A_Chase
		ATHG B 1 A_Chase("","")
		ATHG C 1 A_Chase
		ATHG C 1 A_Chase("","")
		ATHG C 1 A_Chase
		ATHG C 1 A_Chase("","")
		ATHG D 1 A_Chase
		ATHG D 1 A_Chase("","")
		ATHG D 1 A_Chase
		ATHG D 1 A_Chase("","")
		ATHG E 1 A_Chase
		ATHG E 1 A_Chase("","")
		ATHG E 1 A_Chase
		ATHG E 1 A_Chase("","")
		Loop
	Missile:
		ATHG F 0 A_JumpIf(user_spent>=12,"Reload")
		ATHG FGG 4 Fast A_FaceTarget
		ATHG G 0 A_SetUserVar("user_spent",user_spent+1)
		ATHG G 0 A_PlaySound("AK47/EnemyFire",CHAN_WEAPON)
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer",39,0,-4)
		ATHG G 3 A_FaceTarget
		ATHG G 0 A_SetUserVar("user_spent",user_spent+1)
		ATHG G 0 A_PlaySound("AK47/EnemyFire",CHAN_WEAPON)
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer",39,0,0)
		ATHG G 3 A_FaceTarget
		ATHG G 0 A_SetUserVar("user_spent",user_spent+1)
		ATHG G 0 A_PlaySound("AK47/EnemyFire",CHAN_WEAPON)
		ATHG G 0 A_SpawnItemEx("RifleCasing",24,-32,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ATHG G 2 BRIGHT A_CustomMissile("EnemyARTracer",39,0,4)
		ATHG H 3
		ATHG F 4
		Goto See
	Reload:
		ATHG F 13 A_PlaySound("AK47/Clipout",CHAN_WEAPON)
		ATHG H 10 A_PlaySound("AK47/Clipin",CHAN_WEAPON)
		ATHG F 6
		ATHG H 4 A_PlaySound("AK47/Cock",CHAN_WEAPON)
		ATHG F 5 A_SetUserVar("user_spent",0)
		Goto See
	Pain.PlayerPistol:
	Pain.PlayerUzi:
	Pain.PlayerShotgun:
		ATHG IIIIIIIIII 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,32,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(3.0,6.0),frandom(0.0,359.9),SXF_NOCHECKPOSITION)
		ATHG I 3 A_PlaySound("Bullet/HitMetal")
		Goto See
	Pain.PlayerTaser:
		ATHG I 0
		Goto Tased
	Pain:
		ATHG I 3 A_Pain
		Goto See
	Death:
		ATHG J 3 A_Scream
		ATHG KL 3
		ATHG M 3 A_NoBlocking
		ATHG NO 3
		ATHG P 3 A_PlaySound("Misc/Bodyfall")
		ATHG Q -1
		Stop
	}
}